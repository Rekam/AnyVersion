apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
}

version = "1.0-alpha"
group = "com.github.yoojia"

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: LOCAL_RELEASE_URL) {
            authentication(userName: LOCAL_USER, password: LOCAL_PASS)
        }

        snapshotRepository(url: LOCAL_SNAPSHOT_URL) {
            authentication(userName: LOCAL_USER, password: LOCAL_PASS)
        }

        pom.artifactId = 'anyversion'

        pom.project {
            name 'AnyVersion'
            packaging 'aar'
            description 'An android update library'
            url 'https://coding.net/u/yoojia/p/AnyVersion/git'

            scm {
                url 'scm:https://coding.net/yoojia/AnyVersion.git'
                connection 'scm:https://coding.net/yoojia/AnyVersion.git'
                developerConnection 'scm:https://coding.net/yoojia/AnyVersion.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'yoojia'
                    name 'Yoojia.Chen'
                    email 'Yoojia.Chen@gmail.com'
                }
            }
        }
    }
}
